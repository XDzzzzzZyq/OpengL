#version 460 core

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
layout(rgba16f, binding = 2) uniform image2D U_normal;
layout(rgba16f, binding = 3) uniform image2D U_pos;
layout(r16f,    binding = 4) uniform image2D U_Lighting;
layout(rgba8  , binding = 5) uniform image2D U_alpha;
uniform sampler2D U_opt_flow;
uniform sampler2D Shadow_Map;

uniform vec3 U_offset[16];
uniform int frame;
uniform float light_far;
uniform float update_rate;
uniform vec3 offset;
uniform float radius;
uniform vec3 dir;

layout(std430, binding = 6) readonly buffer SDF {
	vec3 SDF_pos;
	vec3 SDF_scale;
	vec3 SDF_size;
	int SDF_subdiv;
	uint SDF_data[];
};

// This shader partially refered to the Repo: https://github.com/GPUOpen-Effects/TressFX/tree/master

float LinearInterpolate(float a, float b, float t)
{
    return a * (1.0f - t) + b * t;
}


//    x = BilinearInterpolate(a, b, c, d, p, q)
//      = LinearInterpolate(LinearInterpolate(a, b, p), LinearInterpolate(c, d, p), q)
float BilinearInterpolate(float a, float b, float c, float d, float p, float q)
{
    return LinearInterpolate( LinearInterpolate(a, b, p), LinearInterpolate(c, d, p), q );
}

//		x = TrilinearInterpolate(a, b, c, d, e, f, g, h, p, q, r)
//		  = LinearInterpolate(BilinearInterpolate(a, b, c, d, p, q), BilinearInterpolate(e, f, g, h, p, q), r)
float TrilinearInterpolate(float a, float b, float c, float d, float e, float f, float g, float h, float p, float q, float r)
{
    return LinearInterpolate(BilinearInterpolate(a, b, c, d, p, q), BilinearInterpolate(e, f, g, h, p, q), r);
}

uint FloatFlip3(float fl)
{
    uint f = floatBitsToUint(fl);
    return (f << 1) | (f >> 31);		//Rotate sign bit to least significant
}
float IFloatFlip3(uint f2)
{
    uint u = (f2 >> 1) | (f2 << 31);
	return uintBitsToFloat(u);
}

float ReadSDFAt(ivec3 index){
	int index_linear = int(index.x + index.y * SDF_size.x + index.z * SDF_size.x * SDF_size.y);
	return IFloatFlip3(SDF_data[index_linear]);
}

float ReadSDF(vec3 pos){
	vec3 transf = (pos - SDF_pos) / SDF_scale;  // normalize
	vec3 over = transf - clamp(transf, -1, 1);  // over the field
	transf = clamp(transf, -1, 1) + 1;			// [0, 2]
	transf *= 0.5;								// [0, 1]
	transf *= (SDF_size - 1);					// [(0,0,0), (W-1,D-1,H-1)]


	float distance_000 = ReadSDFAt(ivec3(transf) + ivec3(0,0,0));
	float distance_001 = ReadSDFAt(ivec3(transf) + ivec3(0,0,1));
	float distance_010 = ReadSDFAt(ivec3(transf) + ivec3(0,1,0));
	float distance_011 = ReadSDFAt(ivec3(transf) + ivec3(0,1,1));
	float distance_100 = ReadSDFAt(ivec3(transf) + ivec3(1,0,0));
	float distance_101 = ReadSDFAt(ivec3(transf) + ivec3(1,0,1));
	float distance_110 = ReadSDFAt(ivec3(transf) + ivec3(1,1,0));
	float distance_111 = ReadSDFAt(ivec3(transf) + ivec3(1,1,1));

	vec3 interp = fract(transf);

	return TrilinearInterpolate(distance_000, distance_100, distance_010, distance_110,
                                distance_001, distance_101, distance_011, distance_111,
                                interp.x, interp.y, interp.z) + length(over);
}

float evaluateShadow(float c_depth, float min_depth){
	float bias = 0.01;
	float delta = c_depth - min_depth - bias;  // delta greater/equal than 0

	return float(delta < 0);
}

vec3 GetCoord(vec3 pos, mat4 trans){

	vec4 pos_p = trans * vec4(pos, 1);
	pos_p.xyz = pos_p.xyz/pos_p.w;
	vec3 coord = vec3(pos_p) * 0.5 + 0.5;
	
	return coord;
}

float shadow_at(vec3 pos, vec3 norm){
	vec3 dir_L = -normalize(dir);
	float shadow = 1.0f;
	float len = 5;

	float bias = 0.01;
	float sdf_b = 1e20;
	float s0 = bias/max(0.001, dot(norm, dir_L));

	for(float s = s0; s<len;){
		float sdf = abs(ReadSDF(pos +dir_L*s));  // Pos -> Light(infin)

		if(sdf < bias){
			return 0;
		}

		float y = sdf*sdf/(2.0*sdf_b);
        float d = sqrt(abs(sdf*sdf-y*y));

		//shadow = min(shadow, wl/w);
		shadow = min( shadow, d/(s*radius) );

		s += sdf;
		sdf_b = sdf;
	}
	return shadow;
}

void UpdateOF(ivec2 uv){

	float Cache_B = imageLoad(U_Lighting, uv).r;
	vec2 OF = texture(U_opt_flow, (vec2(uv)+0.5)/imageSize(U_Lighting)).rg;

	imageStore(U_Lighting, ivec2(uv + OF*imageSize(U_Lighting)), vec4(vec3(Cache_B), 1));
	memoryBarrier();
}

float BOF_sample(ivec2 uv){

	vec2 OF = texture(U_opt_flow, (vec2(uv)+0.5)/imageSize(U_Lighting)).rg;
	ivec2 uv_b = uv + ivec2(OF * imageSize(U_Lighting));
	return imageLoad(U_Lighting, uv_b).r;

}

void main(){

	ivec2 uv = ivec2(gl_GlobalInvocationID.xy);

	//UpdateOF(uv);

	float alpha = imageLoad(U_alpha, uv).r;
		if(alpha < 0.5){
			imageStore(U_Lighting, uv, vec4(1));
			return;
	}

	
	vec3 pos = imageLoad(U_pos, uv).xyz;
	vec3 norm = imageLoad(U_normal, uv).xyz;
	float shadow = shadow_at(pos, norm);	
	float shadow_b = BOF_sample(uv);
	shadow = shadow_b + update_rate * (shadow - shadow_b);

	imageStore(U_Lighting, uv, vec4(vec3(shadow), 1));
}